apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "dummydomain.yetanothercallblocker"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 5100
        versionName "0.5.10"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'dummydomain.yetanothercallblocker.EventBusIndex']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "app_id", variant.applicationId
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

greendao {
    schemaVersion 1
}

dependencies {
    def eventbus_version = '3.2.0'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    runtimeOnly 'com.github.tony19:logback-android:2.0.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
    //noinspection GradleDependency: 3.12.* is the latest version compatible with Android <5
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.gitlab.xynngh:LibPhoneNumberInfo:5b0b9fcbee'
    implementation 'org.apache.commons:commons-csv:1.8'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation "org.greenrobot:eventbus:$eventbus_version"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventbus_version"
}
